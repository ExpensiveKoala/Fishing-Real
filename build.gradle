plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.5-SNAPSHOT" apply false

    id "com.modrinth.minotaur" version "2.7+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.matthewprenger.cursegradle"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-$rootProject.parchment_minecraft:$rootProject.parchment_version@zip")
        }
    }


    var env = System.getenv()
    var modLoader = name

    var changelogFile = rootProject.file("changelog.md")
    var changelogText = changelogFile.exists() ? changelogFile.text : ""

    if (env.MODRINTH_TOKEN) {
        modrinth {
            projectId = env.MODRINTH_PROJECT
            token = env.MODRINTH_TOKEN
            uploadFile = tasks.remapJar
            gameVersions = [minecraft_version]
            loaders = [modLoader]
            changelog = changelogText
        }
    }

    if (env.CURSE_TOKEN) {
        curseforge {
            apiKey = env.CURSE_TOKEN
            project {
                id = env.CURSE_PROJECT
                changelog = changelogText
                releaseType = "release"
                addGameVersion minecraft_version
                mainArtifact(tasks.remapJar)
                if (modLoader == "fabric") {
                    addGameVersion modLoader.capitalize()
                } else {
                    addGameVersion "NeoForge"
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.minecraft_version + "-" + rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url "https://nexus.resourcefulbees.com/repository/maven-public/" }
        maven { url "https://girafi.dk/maven/" } //Aquaculture
        maven { url "https://maven.neoforged.net/releases/" }
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
